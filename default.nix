{
  stdenv, fetchgit, lib,
  lsb-release, which, writeScriptBin,
  # Options: wakunode1, wakunode2, wakubridge
  makeTargets ? [ "wakunode2" ],
  # WARNING: CPU optmizations that make binary not portable.
  nativeBuild ? false,
}:

stdenv.mkDerivation rec {
  pname = "nwaku";
  version = "0.11";
  commit = "fec139748";
  name = "${pname}-${version}-${commit}";

  src = ./.;

  buildInputs = let
    fakeGit = writeScriptBin "git" "echo $commit";
  in [ fakeGit lsb-release which ];

  enableParallelBuilding = true;

  # Avoid make calling 'git describe'.
  GIT_VERSION = version;

  NIMFLAGS = lib.optionalString (!nativeBuild) " -d:disableMarchNative";

  makeFlags = makeTargets;

  preBuildPhases = [ "buildCompiler" ];

  # Generate vendor/.nimble contents with correct paths.
  configurePhase = ''
    ls -l vendor
    export EXCLUDED_NIM_PACKAGES=""
    export NIMBLE_LINK_SCRIPT=$PWD/vendor/nimbus-build-system/scripts/create_nimble_link.sh
    export NIMBLE_DIR=$PWD/vendor/.nimble
    export PWD_CMD=$(which pwd)
    patchShebangs scripts > /dev/null
    patchShebangs $PWD/vendor/nimbus-build-system/scripts > /dev/null
    for dep_dir in $(find vendor -type d -maxdepth 1); do
        pushd "$dep_dir" >/dev/null
        $NIMBLE_LINK_SCRIPT "$dep_dir"
        popd >/dev/null
    done
  '';

  # Nimbus uses it's own specific Nim version bound as a Git submodule.
  buildCompiler = ''
    # Necessary for nim cache creation
    export HOME=$PWD
    make -j$NIX_BUILD_CORES build-nim
    export PATH="$PWD/vendor/nimbus-build-system/vendor/Nim/bin:$PATH"
  '';

  installPhase = ''
    mkdir -p $out/bin
    cp build/* $out/bin
  '';

  meta = with lib; {
    homepage = "https://waku.org/";
    downloadPage = "https://github.com/status-im/nwaku/releases";
    changelog = "https://github.com/status-im/nwaku/blob/master/CHANGELOG.md";
    description = "Waku is the communication layer for Web3. Decentralized communication that scales.";
    longDescription = ''
      Waku is a suite of privacy-preserving, peer-to-peer messaging protocols.
      It removes centralized third parties from messaging, enabling private,
      secure, censorship-free communication with no single point of failure.
      It provides privacy-preserving capabilities, such as sender anonymity,
      metadata protection and unlinkability to personally identifiable information.
      Designed for generalized messaging, enabling human-to-human, machine-to-machine or hybrid communication.
    '';
    branch = "stable";
    license = with licenses; [ asl20 mit ];
    maintainers = with maintainers; [ jakubgs ];
    platforms = with platforms; x86_64 ++ arm ++ aarch64;
  };
}
